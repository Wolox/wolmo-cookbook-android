apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'idea'
apply plugin: 'jacoco-android'
apply from: 'keystore.gradle'
apply from: '../jacoco.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'
apply from: '../scripts/sanity/sanity_task.gradle'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "ar.com.wolox.android.cookbook"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    // Change the "default" to whatever fits. At least one 'dimension' is required by gradle.
    flavorDimensions "default"

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] Wolmo Android Cookbook"
        }

        production {
            resValue "string", "app_name", "Wolmo Android Cookbook"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

jacocoAndroidUnitTestReport {
    excludes = defaultExcludes + ['**/*_Factory.class']
}

buildscript {
    ext.androidx_core_version = '1.0.1'
    ext.androidx_recycler_version = '1.0.0'
    ext.androidx_material_version = '1.0.0'
    ext.androidx_constraint_version = '1.1.3'
    ext.wolmo_core_version = '3.1.0'
    ext.wolmo_networking_version = '3.0.0'
    ext.dagger_version = '2.22.1'
    ext.leak_canary_version = '1.6.2'
    ext.chuck_version = '1.1.0'
    ext.google_play_services_version = '16.0.1'
    ext.fresco_version = '1.13.0'
    ext.nav_arch_component_version = '2.1.0-alpha02'
    ext.rxjava2_version = '2.2.0'
    ext.rxjava2_android_version = '2.1.0'
    ext.facebook_login_version = '4.39.0'
    ext.espresso_core = '3.1.1'
    ext.test_runner = '1.0.1'
    ext.test_rules = '1.1.0'
    ext.mockito_kotlin_version = '2.1.0'
    ext.android_junit = '1.1.0'
    ext.assertj_core = '3.9.0'
    ext.junit_version = '4.12'
    ext.robolectric_version = '4.2'
    ext.fragment_testing_version = '1.1.0-alpha07'
    ext.koin_version = '2.0.1'
    ext.magellan_version = '1.1.0'

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Wolmo
    implementation "com.github.Wolox:wolmo-core-android:$wolmo_core_version"
    implementation "com.github.Wolox:wolmo-networking-android:$wolmo_networking_version"

    // Android support libraries
    implementation "androidx.core:core:$androidx_core_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_recycler_version"
    implementation "com.google.android.material:material:$androidx_material_version"

    // Constraint Layout
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-scope:$koin_version"

    // Fresco
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "com.facebook.fresco:animated-gif:$fresco_version"

    // Third-party
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Google Play Services
    implementation "com.google.android.gms:play-services-auth:$google_play_services_version"

    // Navigation Architecture Component
    implementation "androidx.navigation:navigation-fragment:$nav_arch_component_version"
    implementation "androidx.navigation:navigation-ui:$nav_arch_component_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxjava2_android_version"

    // Facebook Login
    implementation "com.facebook.android:facebook-login:$facebook_login_version"

    // Magellan
    implementation "com.wealthfront:magellan:$magellan_version"
    implementation "com.wealthfront:magellan-support:$magellan_version"
    implementation "com.wealthfront:magellan-rx:$magellan_version"
    implementation "com.wealthfront:magellan-rx2:$magellan_version"
}

dependencies {
    // Unit testing dependencies
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
    testImplementation "org.assertj:assertj-core:$assertj_core"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    testImplementation "org.koin:koin-test:$koin_version"
}

dependencies {
    // Instrumented testing dependencies
    androidTestImplementation "androidx.test.ext:junit:$android_junit"
    androidTestImplementation "androidx.test:rules:$test_rules"
    androidTestImplementation "androidx.test:runner:$test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_core"
    androidTestImplementation "org.assertj:assertj-core:$assertj_core"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_testing_version"
}